#Область ПрограммныйИнтерфейс

Процедура ДополнитьФорму(Форма) Экспорт
		
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицо(Форма);
		ДобавитьГруппуСкидки(Форма);
		ДобавитьПолеСкидки(Форма);
		ДобавитьКомандуРассчитатьСкидку(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицо(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицо(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоПередОснованием(Форма); 
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоПередСуммойДокумента(Форма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьПолеКонтактноеЛицо(Форма)
		
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Н_КонтактноеЛицо";
		
КонецПроцедуры

Процедура ДобавитьПолеКонтактноеЛицоПередОснованием(Форма)
		
    ЭлементВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"),, Форма.Элементы.Основание); 
	ЭлементВвода.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементВвода.ПутьКДанным = "Объект.Н_КонтактноеЛицо";
			
КонецПроцедуры 

Процедура ДобавитьПолеКонтактноеЛицоПередСуммойДокумента(Форма)
		
    ЭлементВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"),, Форма.Элементы.СуммаДокумента); 
	ЭлементВвода.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементВвода.ПутьКДанным = "Объект.Н_КонтактноеЛицо";
			
КонецПроцедуры

Процедура ДобавитьГруппуСкидки(Форма)
		
	ГруппаСкидки = Форма.Элементы.Добавить("ГруппаСкидки", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ГруппаСкидки.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидки.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСкидки.ОтображатьЗаголовок = Ложь;
	ГруппаСкидки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		
КонецПроцедуры

Процедура ДобавитьПолеСкидки(Форма)
		
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидки);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Н_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "РассчитатьСкидкуПриИзменении");
		
КонецПроцедуры

Процедура ДобавитьКомандуРассчитатьСкидку(Форма)
	
	Команда = Форма.Команды.Добавить("РассчитатьСкидку");
	Команда.Заголовок = "Рассчитать скидку";
	Команда.Действие = "РассчитатьСкидкуНаСервере";
	
	НовыйЭлемент = Форма.Элементы.Добавить("РассчитатьСкидку", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидки);
	НовыйЭлемент.Заголовок = "Рассчитать скидку";
	НовыйЭлемент.ИмяКоманды = "РассчитатьСкидку";
	
КонецПроцедуры

#КонецОбласти
